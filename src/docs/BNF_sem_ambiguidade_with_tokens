#        GRAMÃTICA ATUALIZADA EBNF ---> BNF WITH OUR TOKENS SEM AMBIGUIDADE

#        prog -> main conj_classes
#
#        main -> CLASS ID LKEY PUBLIC STATIC VOID MAIN LPAREN STRING LBRACKET RBRACKET ID RPAREN
#        LKEY cmd1 RKEY RKEY
#
#        conj_classes -> EPISILON
#        | conj_classes classe
#
#        classe -> CLASS ID extension LKEY conj_var conj_metodos RKEY
#
#        extension -> EPISILON 
#        | EXTENDS ID
#
#        conj_var -> EPISILON | conj_var var
#
#        conj_metodos -> EPISILON | conj_metodos metodo
#
#        var -> tipo ID ;
#
#        metodo -> PUBLIC tipo ID LPAREN params RPAREN LKEY conj_var conj_cmd RETURN exp ; RKEY
#
#        conj_cmd -> EPISILON 
#        | conj_cmd cmd
#
#        params -> EPISILON 
#        | conj_params
#
#        conj_params -> tipo ID mais_param
#
#        mais_param -> EPISILON
#        | mais_param , tipo ID
#
#        tipo -> INT LBRACKET RBRACKET
#        | BOOLEAN
#        | INT
#        | ID
#        
#        cmd1 -> LKEY conj_cmd RKEY
#        | IF LPAREN exp RPAREN cmd1
#        | IF LPAREN exp RPAREN cmd2 ELSE cmd1
#        | WHILE LPAREN exp RPAREN cmd1
#        | sYSTEM.OUT.PRINTLN LPAREN exp RPAREN ;
#        | ID ATTR exp ;
#        | ID LBRACKET exp RBRACKET ATTR exp ;
#
#        cmd2 -> LKEY conj_cmd RKEY
#        | IF LPAREN exp RPAREN cmd2 ELSE cmd2
#        | WHILE LPAREN exp RPAREN cmd2
#        | SYSTEMOUTPRINTLN LPAREN exp RPAREN ;
#        | ID ATTR exp ;
#        | ID LBRACKET exp RBRACKET ATTR exp ;
#          
#        exp -> exp && rexp
#        | rexp
#
#        rexp -> rexp LTHAN aexp
#        | rexp EQUALS aexp
#        | rexp NEQUALS aexp
#        | aexp
#
#        aexp -> aexp PLUS mexp
#        | aexp MINUS mexp
#        | mexp
#
#        mexp -> mexp TIMES sexp
#        | sexp
#
#        sexp -> NOT sexp
#        | MINUS sexp
#        | TRUE
#        | FALSE
#        | NUM
#        | NULL
#        | NEW INT LBRACKET exp RBRACKET
#        | pexp . LENGTH
#        | pexp LBRACKET exp RBRACKET
#        | pexp
#
#        pexp -> ID
#        | THIS
#        | NEW ID LPAREN RPAREN
#        | LPAREN exp RPAREN
#        | pexp . ID
#        | pexp . ID LPAREN option_exps RPAREN
#
#        option_exps -> EPISILON 
#        | exp 
#
#        exps -> exp conj_exps
#
#        conj_exps -> EPISILON | conj_exps , exp
